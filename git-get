#!/usr/bin/perl
use strict;

my ($what, @args) = @ARGV;

_usage() unless defined $what;

$what =~ tr/-/_/;

{ no strict 'refs';
  if (defined &$what) { print $what->(@args), $ENV{GIT_GET_NO_NEWLINE} ? "" : "\n" }
  else {
    _usage($what);
  }
}

sub current_branch_name {
  _run("git rev-parse --symbolic-full-name --abbrev-ref HEAD");
}

sub branch_remote {
  my ($branch) = @_;
  $branch ||= current_branch_name();
  _run("git config --get 'branch.$branch.remote'");
}

sub branch_remote_branch {
  my ($branch) = @_;
  $branch ||= current_branch_name();
  my $remote = branch_remote($branch);
  if ($remote) { return "$remote/$branch" } else { exit 1 }
}

sub working_tree_dirty {
  my ($arg) = @_;
  for my $cmd ("git rev-parse --verify HEAD > /dev/null",
	       "git update-index --ignore-submodules --refresh > /dev/null",
	       "git diff-files --quiet --ignore-submodules",
	       "git diff-index --cached --quiet HEAD --ignore-submodules") {
      system($cmd) == 0 or return 1;
  }
  return 0;
}

sub heads {
  my @heads;
  for (qx{git branch}) {
    s/^..//; # Trim off leading '*' and/or spaces
    push @heads, $_;
  }
  chomp $heads[-1]; # wrapper will append an extra newline on output
  @heads;
}

sub is_remote {
  my ($in) = @_;
  $in .= "\n";
  if (grep $_ eq $in, qx{git remote}) { exit 0 } else { exit 1 }
}

sub is_ancestor_of {
  my ($A, $B) = @_;
  $A = _run("git", "rev-parse", $A);
  my $merge_base = _run("git", "merge-base", $A, $B);
  exit($A eq $merge_base ? 0 : 1);
}

sub _run {
  my @cmd = @_;
  open my($p), "-|", @cmd or die "Couldn't run '@cmd': $!";
  my $res = join "", <$p>;
  chomp($res);
  return $res;
}

sub _usage {
  my ($what) = @_;
  warn "Don't know how to get '$what'; try:\n" if defined $what;
  my @names = sort grep !/^_/ && defined &{$_}, keys %main::;
  tr/_/-/ for @names;
  print STDERR join "\n", @names, "";
  exit;
}
