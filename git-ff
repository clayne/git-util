#!/usr/bin/perl
#
# Usage:
#  git-ff   (fast-forward HEAD to corresponding remote tracking branch)
#  git-ff refname   (fast-forward ref to corresponding remote tracking branch)
#  git-ff refname [target] (fast-forward ref to target)
#  git-ff -r remote (fast-forward all heads that track this remote)

use strict;
use Getopt::Std;
sub error;

my %opt = (v => 0, r => undef);
getopts('vr:', \%opt) or usage();
$ENV{GIT_GET_NO_NEWLINE} = 1;
my $HEAD = qx{git get current-branch-name};

my $remote_name = $opt{r};
if ($remote_name) {
  system("git get is-remote '$remote_name'") == 0
    or error "Unknown remote '$remote_name'";
  for my $head (qx{git get heads}) {
    chomp $head;
    my $tracking = qx{git-get branch-remote '$head'};
    next unless $tracking eq $remote_name;
    my $remote_branch = qx{git-get branch-remote-branch '$head'};
    ff_ref($head, $remote_branch);
  }
} else {
  my ($refname, $target) = @ARGV;
  $refname ||= qx{git get current-branch-name}
    or error "Can't fast-forward detached HEAD";
  $refname or usage();
  $target ||= qx{git get branch-remote-branch '$refname'}
    or error "Can't find upstream branch for '$refname'";
  ff_ref($refname, $target, "die on failure");
}

sub ff_ref {
  my ($ref, $target, $die_on_failure) = @_;
  my $is_head = $HEAD && $HEAD eq $ref;
  if ($is_head && system("git get is-working-tree-dirty") == 0) {
    error("Won't update current branch '$ref' with dirty working tree");
    exit 1 if $die_on_failure;
    return;
  }
  unless (system("git", "get", "is-ancestor-of", $ref, $target) == 0) {
    die "Can't fast-forward $ref to $target\n" if $die_on_failure;
    return;
  }
  return if system("git", "get", "is-same-object", $ref, $target) == 0;
  if ($is_head) {
    system("git", "merge", "--ff-only", $ref, $target) == 0;
    return;
  }
  chomp($ref = qx{git rev-parse --symbolic-full-name '$ref'});
  $ref || die "Can't find symbolic-full-name of '$ref'\n";
  exit 2 unless system("git", "update-ref", "-m", "merge $target: Fast-forward", $ref, $target) == 0;
  warn "$ref -> $target\n" if $opt{v};
}

sub error {
  warn @_, "\n";
  exit 1;
}
